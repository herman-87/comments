buildscript {
    dependencies {
        classpath("org.openapitools:openapi-generator-gradle-plugin:7.4.0")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.4.0'
    id 'com.google.cloud.tools.jib' version '3.4.1'
}

group = 'com.fastRelay'
version = '0.0.2-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // datasource
    runtimeOnly 'com.h2database:h2'
    implementation 'org.postgresql:postgresql:42.7.5'
    implementation 'org.liquibase:liquibase-core:4.30.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // openapi
    implementation('io.swagger:swagger-annotations:1.6.8')
    implementation('org.openapitools:jackson-databind-nullable:0.2.3')
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2')

    // web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework:spring-webflux'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")

    // mapstruct
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor("org.mapstruct:mapstruct-processor:1.6.3")
    testAnnotationProcessor("org.mapstruct:mapstruct-processor:1.6.3")

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "spring"
    templateDir = "$rootDir/openapi/templates/spring-boot".toString()
    inputSpec = "$rootDir/openapi/main.yaml".toString()
    outputDir = layout.buildDirectory.file("generated/sources/openapi").get().asFile.path
    apiPackage = "com.fastrelay.comment.api"
    modelPackage = "com.fastrelay.comment.dto"
    configOptions = [
            dateLibrary         : "java8-localdatetime",
            library             : "spring-boot",
            interfaceOnly       : "true",
            useTags             : "true",
            skipDefaultInterface: "true",
            useSpringBoot3      : "true"
    ]
    typeMappings = [
            time: "java.time.LocalTime",
            dateTime: "java.time.LocalDateTime"
    ]
}

jib {
    from {
        image = "eclipse-temurin:21-jre"
    }
    to {
        image = "herman87/fastrelay_comment:${project.version}"
        tags = ["${project.version}"]
    }
}

compileJava.dependsOn(
        tasks.openApiGenerate
)
sourceSets.main.java.srcDir layout.buildDirectory.file("generated/sources/openapi/src/main/java").get().asFile.path
